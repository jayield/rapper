plugins {
    id "org.sonarqube" version "2.6.1"
}

group 'com.github.jayield'
version '1.0.5'

tasks.whenTaskAdded {task ->
    if(task.name.contains("signArchives")) {
        task.enabled = false
    }
}

apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'jacoco'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    //compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.2.0'
    compile 'io.vertx:vertx-jdbc-client:3.5.2'
    // https://mvnrepository.com/artifact/io.vertx/vertx-core
    compile group: 'io.vertx', name: 'vertx-core', version: '3.5.2'


    testCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.4.0'
    testCompile group: 'org.hsqldb', name: 'sqltool', version: '2.4.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.github.jayield'
            artifactId 'rapper'
            version '1.0.5'

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            /*credentials {
                username sonatypeUsername
                password sonatypePassword
            }*/
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Async DataMapper for JDBC'
            name 'Rapper'
            url 'https://github.com/jayield/rapper'
            organization {
                name 'com.github.jayield'
                url 'https://github.com/jayield'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/jayield/rapper/issues'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/jayield/rapper/blob/master/license.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/jayield/rapper'
                connection 'scm:git:git://github.com/jayield/rapper.git'
                developerConnection 'scm:git:ssh://git@github.com:jayield/rapper.git'
            }
            developers {
                developer {
                    name 'Diogo'
                }
                developer {
                    name 'Jo√£o'
                }
                developer {
                    name 'Tiago'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

signing {
    sign configurations.archives
}